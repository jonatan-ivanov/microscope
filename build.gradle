buildscript {
    ext {
        springBootVersion = '1.5.13.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.bmuschko:gradle-docker-plugin:3.1.0'
    }
}
plugins {
    id 'com.gorylenko.gradle-git-properties' version '1.5.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-java-application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'microscope.Microscope'

springBoot  {
    buildInfo()
}

repositories {
    mavenCentral()
}
dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3'
    }
}
dependencies {
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    compile 'de.codecentric:spring-boot-admin-server:1.+'
    compile 'de.codecentric:spring-boot-admin-server-ui:1.+'
    compile 'de.codecentric:spring-boot-admin-server-ui-hystrix:1.+'
    compile 'org.jolokia:jolokia-core'

    compileOnly 'org.projectlombok:lombok'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

docker {
    javaApplication {
        baseImage = 'openjdk:8'
        tag = "$project.dockerNamespace/$project.applicationName:$project.version"
        maintainer = project.maintainer
        ports = [8090]
    }
    registryCredentials {
        username = dockerRegistryUserName
        password = dockerRegistryPassword
    }
}

task cleanAll {
    dependsOn clean, cleanIdea, cleanIdeaWorkspace
    doLast {
        delete 'out', 'logs'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionType = Wrapper.DistributionType.ALL
}
